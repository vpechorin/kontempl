buildscript {
    ext {
        springBootVersion = '1.5.7.RELEASE'
    }
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url 'http://repo.spring.io/libs-release-remote/' }
        maven { url 'https://plugins.gradle.org/m2/' }
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion")
        classpath 'org.hidetake:gradle-ssh-plugin:2.9.0'
        classpath "net.ltgt.gradle:gradle-apt-plugin:0.12"
    }
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'org.hidetake.ssh'
apply plugin: "net.ltgt.apt"

group = 'net.pechorina.kontempl'
version = '2.0.0'

description = "Kontempl - web site management system"

sourceCompatibility = 1.8
targetCompatibility = 1.8

jar {
    baseName = 'kontempl'
    version = "$version"
}

sourceSets.main.java.srcDirs = []
sourceSets.main.groovy.srcDirs += ["src/main/java"]

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'http://repo.spring.io/libs-release-remote/' }
    maven { url 'http://repo.spring.io/libs-release-remote/' }
}

dependencies {
    testCompile('org.springframework.boot:spring-boot-starter-test') {
        exclude(module: 'commons-logging')
    }

	compile 'org.codehaus.groovy:groovy-all'
    compile group: 'mysql', name: 'mysql-connector-java'
	compile('org.springframework.boot:spring-boot-starter-web')
    compile 'org.springframework.boot:spring-boot-starter-actuator'
    compile ('org.springframework.boot:spring-boot-starter-data-jpa') {
        exclude(module: "hibernate-entitymanager")
        exclude(module: "hibernate-core")
    }

    compile group: 'org.hibernate', name: 'hibernate-core', version: '5.2.10.Final'

    compile 'org.springframework.boot:spring-boot-starter-security'
    compile 'org.springframework.boot:spring-boot-starter-logging'
    compile 'org.springframework.boot:spring-boot-starter-cache'
    compile 'org.springframework.boot:spring-boot-starter-mail'
    compile 'org.springframework:spring-context-support'
    compile 'com.fasterxml.jackson.core:jackson-databind'
    compile 'com.fasterxml.jackson.datatype:jackson-datatype-joda'
    compile ('org.jadira.usertype:usertype.core:6.0.1.GA')
    compile 'joda-time:joda-time'
    compile('org.apache.httpcomponents:httpclient') {
		exclude(module: 'commons-logging')
    }
    compile group: 'com.github.ben-manes.caffeine', name: 'caffeine', version: '2.5.6'
    compile 'org.hibernate:hibernate-validator'

    compile group: 'org.flywaydb', name: 'flyway-core', version: "4.2.0"
    compile group: 'commons-codec', name: 'commons-codec'
    compile group: 'com.google.guava', name: 'guava', version:'23.0'
    compile group: 'org.jsoup', name: 'jsoup', version:'1.7.3'
    compile 'commons-fileupload:commons-fileupload:1.3.1'
    compile 'commons-io:commons-io:2.4'
    compile 'org.apache.commons:commons-lang3:3.3.2'
    compile 'org.imgscalr:imgscalr-lib:4.3-SNAPSHOT'
    compile group: 'com.twelvemonkeys.imageio', name: 'imageio-core', version: '3.3.2'
    compile group: 'com.twelvemonkeys.imageio', name: 'imageio-jpeg', version: '3.3.2'
    compile group: 'com.twelvemonkeys.imageio', name: 'imageio-tiff', version: '3.3.2'
    compile group: 'com.twelvemonkeys.imageio', name: 'imageio-metadata', version: '3.3.2'
    compile group: 'com.github.dfabulich', name: 'sitemapgen4j', version:'1.0.6'
	compile 'net.sf.uadetector:uadetector-resources:2014.10'
	compile group: 'javax.validation', name: 'validation-api'
    compile group: 'org.mapstruct', name: 'mapstruct-jdk8', version: '1.2.0.Final'
    apt "org.mapstruct:mapstruct-processor:1.2.0.Final"

}

remotes {
    web01 {
        role 'remoteweb'
        host = "$kontemplUploadHost"
        user = "$kontemplUploadUser"
        identity = file(sshIdentity)
    }
}

task reload {
    doLast {
        ssh.run {
            session(remotes.web01) {
                put from: jar.archivePath, into: '/web/kontempl'
                execute 'systemctl restart kontempl'
            }
        }
    }
}

compileGroovy {
    groovyOptions.javaAnnotationProcessing = true
}

bootRun {
    args = ["-Xms256m", "-Xmx256m"]
}